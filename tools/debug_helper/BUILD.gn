# Copyright 2019 the V8 project authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../gni/v8.gni")

config("internal_config") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.

  if (is_component_build) {
    defines = [ "BUILDING_V8_DEBUG_HELPER" ]
  }

  include_dirs = [
    ".",
    "$target_gen_dir",
  ]
}

# This config should be applied to code using v8_debug_helper.
config("external_config") {
  if (is_component_build) {
    defines = [ "USING_V8_DEBUG_HELPER" ]
  }
  include_dirs = [ "." ]
}

v8_component("v8_debug_helper") {
  testonly = true

  public = [
    "debug-helper.h",
  ]

  sources = [
    "$target_gen_dir/../../torque-generated/class-debug-readers-tq.cc",
    "$target_gen_dir/../../torque-generated/class-debug-readers-tq.h",
    "debug-helper-internal.h",
    "debug-helper.h",
    "find-roots.cc",
    "get-object-properties.cc",
    "heap-constants.cc",
    "heap-constants.h",
  ]

  deps = [
    "../..:run_torque",
    "../..:v8_headers",
    "../..:v8_libbase",
  ]

  configs = [ ":internal_config" ]
  if (v8_enable_i18n_support) {
    configs += [ "//third_party/icu:icu_config" ]
  }

  public_configs = [ ":external_config" ]
}
