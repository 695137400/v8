// Copyright 2019 the V8 project authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

namespace torque_internal {
  // TODO(gsps): Synthesize SizeOf<T> in the compiler
  // TODO(gsps): Add failure labels to Next() and Access()

  macro SizeOf<T: type>(): constexpr int31;
  SizeOf<Smi>(): constexpr int31 {
    return kTaggedSize;
  }

  struct Unsafe {}

  struct Reference<T: type> {
    const object: HeapObject;
    const offset: intptr;
    unsafeMarker: Unsafe;
  }

  macro IsReferenceEqual<T: type>(a:&T, b:&T): bool {
    return SameValue(a.object, b.object) && a.offset == b.offset;
  }

  macro UnsafeNewReference<T: type>(object: HeapObject, offset: intptr):&T {
    return Reference<T>{
      object: object,
      offset: offset,
      unsafeMarker: Unsafe {}
    };
  }

  struct Slice<T: type> {
    Access(index: intptr):&T {
      if (Convert<uintptr>(index) < Convert<uintptr>(this.length)) {
        return UnsafeNewReference<T>(
            this.base.object, this.base.offset + index * SizeOf<T>());
      } else {
        unreachable;
      }
    }

    Iterator(): SliceIterator<T> {
      const end = UnsafeNewReference<T>(
          this.base.object, this.base.offset + this.length * SizeOf<T>());
      return SliceIterator{start: this.base, end: end, unsafeMarker: Unsafe {}};
    }

    base:&T;
    length: intptr;
    unsafeMarker: Unsafe;
  }

  macro UnsafeNewSlice<T: type>(base: Reference<T>, length: intptr): Slice<T> {
    return Slice{base: base, length: length, unsafeMarker: Unsafe {}};
  }

  struct SliceIterator<T: type> {
    Empty(): bool {
      return IsReferenceEqual(this.start, this.end);
    }

    Next():&T {
      if (this.Empty()) {
        unreachable;
      } else {
        const result = this.start;
        this.start = UnsafeNewReference<T>(
            this.start.object, this.start.offset + SizeOf<T>());
        return result;
      }
    }

    start:&T;
    end:&T;
    unsafeMarker: Unsafe;
  }

}  // namespace torque_internal
