// Copyright 2019 the V8 project authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

#include 'src/builtins/builtins-regexp-gen.h'

namespace regexp_replace {

  extern builtin
  StringIndexOf(implicit context: Context)(String, String, Smi): Smi;

  extern transitioning macro
  RegExpBuiltinsAssembler::ReplaceSimpleStringFastPath(
      implicit context: Context)(JSRegExp, String, String): String;
  extern transitioning macro
  RegExpBuiltinsAssembler::ReplaceGlobalCallableFastPath(
      implicit context: Context)(JSRegExp, String, Callable): String;

  extern transitioning runtime
  RegExpReplaceRT(Context, JSReceiver, String, Object): String;
  extern transitioning runtime
  StringReplaceNonGlobalRegExpWithFunction(Context, String, JSRegExp, Callable):
      String;

  transitioning builtin RegExpReplace(implicit context: Context)(
      rx: FastJSRegExp, s: String, replaceValue: Object): String {
    // TODO(pwong): Remove assert all callers (StringPrototypeReplace) are from
    // Torque.
    assert(Is<FastJSRegExp>(rx));

    // 2. Is {replace_value} callable?
    typeswitch (replaceValue) {
      case (replaceFn: Callable): {
        if (rx.flagGlobal) {
          return ReplaceGlobalCallableFastPath(rx, s, replaceFn);
        }

        return StringReplaceNonGlobalRegExpWithFunction(
            context, s, rx, replaceFn);
      }
      case (Object): {
        const replaceString: String = ToString_Inline(context, replaceValue);

        try {
          // ToString(replaceValue) could potentially change the shape of the
          // RegExp object. Recheck that we are still on the fast path and bail
          // to runtime otherwise.
          const fastRx: FastJSRegExp = Cast<FastJSRegExp>(rx) otherwise Runtime;

          // TODO(pwong): Use LookupSingleCharacterStringFromCode.
          if (StringIndexOf(replaceString, '$', 0) != -1) goto Runtime;

          return ReplaceSimpleStringFastPath(fastRx, s, replaceString);
        }
        label Runtime deferred {
          return RegExpReplaceRT(context, rx, s, replaceString);
        }
      }
    }
  }

  transitioning javascript builtin RegExpPrototypeReplace(
      context: Context, receiver: Object, ...arguments): Object {
    const methodName: constexpr string = 'RegExp.prototype.@@replace';
    const string: Object = arguments[0];
    const replaceValue: Object = arguments[1];

    // Let rx be the this value.
    // If Type(rx) is not Object, throw a TypeError exception.
    const rx = Cast<JSReceiver>(receiver)
        otherwise ThrowTypeError(kIncompatibleMethodReceiver, methodName);

    // Let S be ? ToString(string).
    const s = ToString_Inline(context, string);

    // Fast-path checks: 1. Is the {receiver} an unmodified JSRegExp instance?
    try {
      const fastRx: FastJSRegExp = Cast<FastJSRegExp>(rx) otherwise Runtime;
      return RegExpReplace(fastRx, s, replaceValue);
    }
    label Runtime deferred {
      return RegExpReplaceRT(context, rx, s, replaceValue);
    }
  }

}
